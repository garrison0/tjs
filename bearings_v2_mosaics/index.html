<!DOCTYPE html>
<html lang="en">

<style>
    #c { 
        width: 100%;
        height: 35rem;
    }
</style>

<script id="vertexShader" type="NOT_JS">
    // Description : Array and textureless GLSL 2D/3D/4D simplex 
    //               noise functions.
    //      Author : Ian McEwan, Ashima Arts.
    //  Maintainer : stegu
    //     Lastmod : 20201014 (stegu)
    //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
    //               Distributed under the MIT License. See LICENSE file.
    //               https://github.com/ashima/webgl-noise
    //               https://github.com/stegu/webgl-noise
    // 

    vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
    }

    vec4 taylorInvSqrt(vec4 r)
    {
        return 1.79284291400159 - 0.85373472095314 * r;
    }

    float snoise(vec3 v)
    { 
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

        // Permutations
        i = mod289(i); 
        vec4 p = permute( permute( permute( 
                    i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
                + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        // Gradients: 7x7 points over a square, mapped onto an octahedron.
        // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
        float n_ = 0.142857142857; // 1.0/7.0
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

        //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        // Mix final noise value
        vec4 m = max(0.5 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 105.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                        dot(p2,x2), dot(p3,x3) ) );
    }

    float random (in vec2 st) {
        return fract(sin(dot(st.xy,
                             vec2(12.9898,78.233)))*
            43758.5453123);
    }
    
    // Based on Morgan McGuire @morgan3d
    // https://www.shadertoy.com/view/4dS3Wd
    float noise (in vec2 st) {
        vec2 i = floor(st);
        vec2 f = fract(st);
    
        // Four corners in 2D of a tile
        float a = random(i);
        float b = random(i + vec2(1.0, 0.0));
        float c = random(i + vec2(0.0, 1.0));
        float d = random(i + vec2(1.0, 1.0));
    
        vec2 u = f * f * (3.0 - 2.0 * f);
    
        return mix(a, b, u.x) +
                (c - a)* u.y * (1.0 - u.x) +
                (d - b) * u.x * u.y;
    }
    
    #define OCTAVES 6
    float fbm (in vec2 st) {
        // Initial values
        float value = 0.0;
        float amplitude = .5;
        float frequency = 0.;
        //
        // Loop of octaves
        for (int i = 0; i < OCTAVES; i++) {
            value += amplitude * noise(st);
            st *= 2.;
            amplitude *= .5;
        }
        return value;
    }

    float mosaic (in vec2 uv, in float t) { 
        // hash texcoords to a tile s,t 
        vec2 numTiles = vec2(54. + 40. * cos(t * 0.2), 37. + 29. * sin(t * 0.2));
        vec2 st = floor(numTiles * uv);
        return noise(st);
    }
    
    #define MOSAIC_OCTAVES 3
    float mosaicOctaves (in vec2 st, in float t) { 
        // Initial values
        float value = 0.0;
        float amplitude = .5;
        float frequency = 0.;
        //
        // Loop of octaves
        for (int i = 0; i < MOSAIC_OCTAVES; i++) {
            value += amplitude * mosaic(st, t);
            st *= 2.;
            amplitude *= .5;
        }
        return value;
    }

    varying vec3 vPosition;
    varying float vNoise;
    varying float vTime;

    uniform float time;

    float turbulence( vec3 p ) {

        float t = -.5;

        for (float f = 1.0 ; f <= 10.0 ; f++ ){
            float power = pow( 2.0, f );
            t += abs( snoise( vec3( power * p ) ) / power );
        }

        return t;

    }

    float pattern( in vec2 p )
    {
        vec2 q = vec2( fbm( p + vec2(0.0,0.0) ),
                    fbm( p + vec2(5.2,1.3) ) );

        vec2 r = vec2( fbm( p + 4.0*q + vec2(1.7,9.2) ),
                    fbm( p + 4.0*q + vec2(8.3,2.8) ) );

        return fbm( p + 4.0*r );
    }

    void main() {

        // vUv = uv;

        // get a turbulent 3d noise using the normal, normal to high freq
        float tNoise = -7.5 * turbulence( -0.255 * normal + ( time / 50.0 ) );
        // get a 3d noise using the position, low frequency
        float b = cos ( cos( cos ( snoise( 0.5 * position * ( time / 30.0 ) ) ) ) );
        // compose both noises
        float displacement = 2.2 * (0.2 * tNoise + 0.3 * b );

        float fbmNoise = fbm(vec2(position.xz + position.y));

        // make a second noise with domain warped fbm
        float displacementFBM = 7.5 * ( 0.2 * fbmNoise + 0.3 * b );

        // make a third noise based on mosaic 
        float mosaicNoise = 3.0 * mosaic(uv, time);

        // interpolate between the three
        float scalar = 0.4 * sin(time / 30.0);
        float scalar2 = 0.4 * cos(time / 30.0);
        float scalar3 = 0.7 * sin(time / 30.0 + 3.141592653 / 4.0) - 0.2;

        if (scalar3 < 0.) { 
            scalar += 1.25 * abs(scalar3);
            scalar2 += 0.5 * abs(scalar3);
            scalar3 = 0.;
        }
        vNoise = scalar * tNoise + scalar2 * fbmNoise + 0.3 * b + scalar3 * mosaicNoise;
        //vNoise = mosaicNoise;
        // move the position along the normal and transform it
        //vec3 newPosition = position - normal * mosaicNoise;
        vec3 newPosition = position - abs(scalar) * (normal * displacement) - abs(scalar2) * (normal * displacementFBM) - abs(scalar3) * (normal * mosaicNoise);
        
        vPosition = newPosition;

        gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );

    }
</script>

<script id="fragmentShader" type="NOT_JS">
    // varying vec2 vUv;
    varying vec3 vPosition;
    varying float vNoise;
    varying float vTime;

    vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
    {  return a + b*cos( 6.28318*(c*t+d) ); }

    void main() {
        vec3 col = palette( ( vNoise / 5.0 ) + vTime * ( vPosition.z / 1.0 + vPosition.x / 1.0 ) ,
                    vec3(0.5,0.5,0.5),
                    vec3(0.5,0.5,0.5),
                    vec3(2.0,1.0,0.0),
                    vec3(0.5,0.2,0.25));

        gl_FragColor = vec4( col * ( 1. - 0.05 * vNoise ), 1.0 );
    }
</script>

<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Document</title>
</head>
<body>
    <canvas id="c"></canvas>
    <script type="module" src="index.js"></script>
</body>
</html>